###########################
#   MANDELBROT ON A GPU   #
###########################
#
# Let's render the mandelbrot set!
#
# The steps are as follows:
#   1. Set up mesh of grid points to render
#   2. Loop over all grid points
#   3. Parallel process num_iters before divergence,
#      according to the Mandelbrot relation, z = z^2 + z_0.
#   4. Send num_iters to HDMI module for colorized rendering.

# CONVENTIONS:
#   - There are 16 private registers for the controller.
#   - Register 0 is always set all to zeros.
#   - Register 1 is reserved as a return line.
#   - There are 2 FMA blocks available for parallelism. [UPDATE TO 16]
#   - All FMA operations are fixed-point with 16 bits.
#   - Negative numbers are represented as two's complement.
#   - There are 504000 bits available in GPU main memory. [UPDATE BRAM TO ACCOUNT]
#   - Main memory is split into lines of 6 numbers each. [UPDATE TO 3*FMA_COUNT]
#   - The HDMI display expects resolution of 160 x 320, with 4 bits per pixel.
#   - The bits represent num_iters / 8, so that the range [0, 127] is represented.

# Step 1: Set up mesh of grid points to render

# DX = 2^(-7) = 0.0078125 so that X_MAX = 0.5 
# DY = 2^(-7) = 0.0078125 so that Y_MAX = 1.25
xor 0 0                   # zero out register 0
addi 2 0 320#8            # HEIGHT = 320: set register 2 to 320
addi 3 0 320#2            # WIDTH = 320: set register 3 to 320
addi 4 0 -1.875f#-1f      # X_MIN = -1.875 (f notation compiles into fixed-point)
addi 5 0 63               # MAX_ITERS = 127 (register 5)
addi 6 0 -1.25f#0f#0.25f  # Y_MIN = -1.25 (f notation compiles into fixed-point)

# FREE REGISTERS: reg 7, reg 12

# Step 2: Loop over all grid points
#   - Use register 8 as x_counter
#   - Use register 9 as y_counter
#   - Use register 10 as x_val
#   - Use register 11 as y_val
#   - Use register 14 as addr_for_iters
#   - Use register 15 for iters
#   - Increment x_val and y_val by DX and DY
# We will implement the following pseudocode:
#   for (x_counter = 0; x_counter < WIDTH; x_counter++) {
#     for (y_counter = 0; y_counter < HEIGHT; y_counter++) {
#        // load Mandelbrot coordinates into main memory
#     }
#   }

xor 14 14              # zero out register 14 (addr_for_iters) [JUMP MARKER 7]

# <FOR LOOP X>

xor 8 8                # set x_counter = 0
addi 10 4 0            # set x_val to X_MIN

bge 8 3                # branch if x_counter >= WIDTH [JUMP MARKER 0]
jump (((1)))           # jump to (1)

# <FOR LOOP X: INNER LOGIC>

# <FOR LOOP Y>

xor 9 9                # set y_counter = 0
addi 11 6 0            # set y_val to Y_MIN

bge 9 2                # branch if y_counter >= HEIGHT [JUMP MARKER 2]
jump (((3)))           # jump to (3)

# <FOR LOOP Y: INNER LOGIC>

# In this inner nested for loop, we have access to the following variables:
#   x_counter (reg 8), y_counter (reg 9), x_val (reg 10), y_val (reg 11)
# We will place these values into memory in such a way that we can enter one
# final for loop, in which we will compute num_iters.

# We will always perform Mandelbrot calculations in the bram_temp_in buffer inside the memory module.
# First, output just x_0 into both FMAs for later use.
load 0 0 0   # load 0 into FMA "a" values (diff 0 per FMA)
load 1 0 0   # load 0 into FMA "b" values (diff 0 per FMA)
load 2 10 0  # load x_val into FMA "c" values
write 1 1    # send loaded buffer to the FMAs! (replace_c, fma_valid_out)
#pause
# Next, output just y_val into both FMAs for later use.
load 2 11 0.0078125f  # load y_0 into FMA "c"      (diff DY per FMA)
write 1 1    # send loaded buffer to the FMAs! (replace_c, fma_valid_out)
#pause
# Finally, let's output the squared magnitude of z_0 = x_0 + y_0.
load 0 10 0  # load x_val into FMA "a" values
load 1 10 0  # load x_val into FMA "b" values
load 2 0 0   # load 0 into FMA "c" values
write 1 0    # send loaded buffer to the FMAs! (replace_c, not fma_valid_out)
load 0 11 0.0078125f # load y_0 into FMA "a"       (diff DY per FMA)
load 1 11 0.0078125f # load y_0 into FMA "b"       (diff DY per FMA)
write 0 1    # send loaded buffer to the FMAs! (not replace c, fma_valid_out)
#pause
nop          # stall for final value to write

# NOW: the write buffer will send THREE values for each FMA:
#   1. x_i
#   2. y_i
#   3. x_0*x_0 + y_0*y_0
# Shuffle should select out (x_i, y_i).
# Shuffle should also select (2x_i, -y_i).

# Register 15 will hold the iter value
#pause
addi 15 0 0   # iters = 0
#pause
bge 15 5      # if iters >= 127 [JUMP MARKER 6]
jump (((5)))  # jump to (5)

# <mandelbrot for loop>

# Three steps:
# 1. Now we pull (x, y) values out of fma_write_buffer via loadb with shuffle arguments.
# 2. We compute the next iteration of the Mandelbrot formula.
# 3. Then we OP_OR to record any new divergences.

# Let's compute x*x + x_0 (TEMP x_{i+1})
loadb 1 1 0    # set buffer to "x_i x_i 0" (1 1 0)
load 2 10 0    # load x_val = x_0 into "c" values
write 1 0      # send loaded buffer to FMAs! (replace_c, not fma_valid_out)

# Let's compute x*x - y*y + y_0 (FULL x_{i+1})
loadb -2 2 0   # set buffer to "-y_i y_i 0" (-2 2 0)
write 0 1      # send loaded buffer to FMAs! (not replace_c, fma_valid_out)
#pause

# Let's compute 2*x*y + y_0 (FULL y_{i+1})
loadb 4 2 0         # set buffer to "2*x_i y_i 0" (4 2 0)
load 2 11 0.0078125f # load y_0 into FMA "c"       (diff DY per FMA)
#pause
write 1 1           # send loaded buffer to FMAs! (replace_c, fma_valid_out)
#pause

# Let's compute x*x + y*y (magnitude of z_i)
loadb 1 1 0    # set buffer to "x_i x_i 0" (1 1 0)
#pause
write 1 0      # send loaded buffer to FMAs! (replace_c, not fma_valid_out)
#pause
loadb 2 2 0    # set buffer to "y_i y_i 0" (2 2 0)
#pause
write 0 1      # send loaded buffer to FMAs! (not replace_c, fma_valid_out)
#pause
nop            # stall for results to flush out from write buffer

# NOW: the write buffer will send THREE values for each FMA:
#   1. x_{i+1}
#   2. y_{i+1}
#   3. x_i*x_i + y_i*y_i

or 15    # record whether points have diverged by iteration iter (reg 15)

# </mandelbrot for loop>

#pause
addi 15 15 1  # increment iters by 1
bge 0 0       # prepare to jump (compare 0 >= 0)
jump (((6)))  # jump to (6) [JUMP MARKER 5]

# SEND THE MANDELBROT_ITERS ARRAY TO FRAME BUFFER AT THE PROPER ADDRESS

senditers 8 9  # send iters to address at (x_val, y_val) of frame buffer

#pause

# </FOR LOOP Y: INNER LOGIC>

addi 11 11 0.125f      # increment y_val by FMA_COUNT*DY
addi 14 14 16          # increment addr_for_iters by FMA_COUNT
addi 9 9 16            # increment y_counter by FMA_COUNT
bge 0 0                # prepare to jump (compare 0 >= 0)
jump (((2)))           # jump to (2) [JUMP MARKER 3]

# </FOR LOOP Y>

# </FOR LOOP X: INNER LOGIC>

addi 10 10 0.0078125f  # increment x_val by DX
addi 8 8 1             # increment x_counter by 1
bge 0 0                # prepare to jump (compare 0 >= 0)
jump (((0)))           # jump to (0) [JUMP MARKER 1]

# </FOR LOOP X>

fbswap  # tell the frame buffer to begin reading in the iters we just computed!

# Increment the num_iters we calculate by 1, then loop back to the start
#addi 5 5 1
#jump (((7)))

end
